
if exists (select * from sysobjects where type = 'P' and name = 'usp_LeagueGetAllBySystemUserID')
	begin
	    drop procedure dbo.usp_LeagueGetAllBySystemUserID
	end
go

create procedure dbo.usp_LeagueGetAllBySystemUserID
 /*******************************************************************************************************
 Logical Advantage, LLC
 www.logicaladvantage.com
 704-377-5066
 --------------------------------------------------------------------------------------------------------
 Stored Procedure:	usp_LeagueGetAllBySystemUserID
 
 Description:		Selects a League record for the given parameters
 
 --------------------------------------------------------------------------------------------------------
 Change Log:
 v1.0.0 - 10/5/2010  Original Release - Generated by LAAF
 ********************************************************************************************************
 To keep the Generator from overwriting this file add the word NOT between the center asterisks ( *NOT* )
 *** DO *NOT* GENERATE ***
 ********************************************************************************************************/
 @SystemUserID int

as

;
with AdminLeagues (LeagueID) as(
		select ul.LeagueID
		from dbo.UserLeague ul 
		where ul.SystemUserID = @SystemUserID
		group by ul.LeagueID
),

	MemberLeagues (LeagueID) as (
	select l.LeagueID
	from dbo.League l
	inner join dbo.TeamSeason ts on ts.LeagueID = l.LeagueID
	inner join dbo.SwimmerTeamSeason sts on sts.TeamSeasonID = ts.TeamSeasonID
	inner join dbo.UserSwimmer us on us.SwimmerID = sts.SwimmerID
	where us.SystemUserID = @SystemUserID
	and l.LeagueID not in (	select ul.LeagueID
							from dbo.UserLeague ul 
							where ul.SystemUserID = @SystemUserID)
)

	--Leagues that SystemUser administrates
	select
		 l.[Description]
		,l.LeagueID
		,l.LeagueName
		,l.RegionID
		,r.RegionAbbrev
		,cast(1 as bit) as IsAdmin
	from dbo.League l
	inner join dbo.Region r on r.RegionID = l.RegionID
	inner join AdminLeagues al on al.LeagueID = l.LeagueID
	
	union 
	
	--Teams that SystemUser has a swimmer involved in
	select
		 l.[Description]
		,l.LeagueID
		,l.LeagueName
		,l.RegionID
		,r.RegionAbbrev
		,cast(0 as bit) as IsAdmin
	from dbo.League l
	inner join dbo.Region r on r.RegionID = l.RegionID
	inner join MemberLeagues ml on ml.LeagueID = l.LeagueID
go

