
if exists (select * from sysobjects where type = 'P' and name = 'usp_TeamGetAllBySystemUserID')
	begin
	    drop procedure dbo.usp_TeamGetAllBySystemUserID
	end
go

create procedure dbo.usp_TeamGetAllBySystemUserID
 /*******************************************************************************************************
 Logical Advantage, LLC
 www.logicaladvantage.com
 704-377-5066
 --------------------------------------------------------------------------------------------------------
 Stored Procedure:	usp_TeamGetAllBySystemUserID
 
 Description:		Selects a Team record for the given parameters
 
 --------------------------------------------------------------------------------------------------------
 Change Log:
 v1.0.0 - 12/8/2010  Original Release - Generated by LAAF
 ********************************************************************************************************
 To keep the Generator from overwriting this file add the word NOT between the center asterisks ( *NOT* )
 *** DO *NOT* GENERATE ***
 ********************************************************************************************************/
 @SystemUserID int

as

;
with AdminTeams (TeamID, UserTeamID) as(
		select ut.TeamID, ut.UserTeamID
		from dbo.UserTeam ut 
		where ut.SystemUserID = @SystemUserID
		group by ut.TeamID, ut.UserTeamID
),

	MemberTeams (TeamID) as (
	select t.TeamID
	from dbo.Team t
	inner join dbo.TeamSeason ts on ts.TeamID = t.TeamID
	inner join dbo.SwimmerTeamSeason sts on sts.TeamSeasonID = ts.TeamSeasonID
	inner join dbo.UserSwimmer us on us.SwimmerID = sts.SwimmerID
	where us.SystemUserID = @SystemUserID
	and t.TeamID not in (	select ut.TeamID
							from dbo.UserTeam ut 
							where ut.SystemUserID = @SystemUserID)
)

	--Teams that SystemUser administrates
	select
		 t.TeamID
		,t.TeamName
		,t.Abbrev
		,t.HomePoolConfigID
		,l.Name as LocationName
		,at.UserTeamID
		,cast(1 as bit) as IsAdmin
	from dbo.Team t
	inner join dbo.PoolConfig pc on pc.PoolConfigID = t.HomePoolConfigID
	inner join dbo.[Pool] p on p.PoolID = pc.PoolID
	inner join dbo.Location l on l.LocationID = p.LocationID
	inner join AdminTeams at on at.TeamID = t.TeamID
	
	union 
	
	--Teams that SystemUser has a swimmer involved in
	select
		 t.TeamID
		,t.TeamName
		,t.Abbrev
		,t.HomePoolConfigID
		,l.Name as LocationName
		,0 as UserTeamID
		,cast(0 as bit) as IsAdmin
	from dbo.Team t
	inner join dbo.PoolConfig pc on pc.PoolConfigID = t.HomePoolConfigID
	inner join dbo.[Pool] p on p.PoolID = pc.PoolID
	inner join dbo.Location l on l.LocationID = p.LocationID
	inner join MemberTeams mt on mt.TeamID = t.TeamID
go

