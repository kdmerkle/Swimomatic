
if exists (select * from sysobjects where type = 'P' and name = 'usp_TeamGetAllBySearch')
	begin
	    drop procedure dbo.usp_TeamGetAllBySearch
	end
go

create procedure dbo.usp_TeamGetAllBySearch
 /*******************************************************************************************************
 Logical Advantage, LLC
 www.logicaladvantage.com
 704-377-5066
 --------------------------------------------------------------------------------------------------------
 Stored Procedure:	usp_TeamGetAllBySearch
 
 Description:		Selects a Team record for the given parameters
 
 --------------------------------------------------------------------------------------------------------
 Change Log:
 v1.0.0 - 12/13/2010  Original Release - Generated by LAAF
 ********************************************************************************************************
 To keep the Generator from overwriting this file add the word NOT between the center asterisks ( *NOT* )
 *** DO *NOT* GENERATE ***
  ********************************************************************************************************/
 @Address varchar(50)
,@City varchar(50)
,@PostalCode varchar(50)
,@RegionID int
,@TeamName varchar(50)

as
	if @Address = '' set @Address = null
	if @City = '' set @City = null
	if @PostalCode = '' set @PostalCode = null
	if @RegionID = 0 set @RegionID = null
	if @TeamName = '' set @TeamName = null
	
	select
		 t.TeamID
		,t.TeamName
		,l.[Address]
		,l.Name as LocationName
		,l.PostalCode
		,l.RegionID
		,l.City
		,t.Abbrev
		,t.HomePoolConfigID
		,ts.TeamSeasonID
		,s.[Description] as SeasonDescription
		,s.StartDate
		,s.EndDate
		,lg.LeagueName
	from dbo.Team t
	inner join dbo.TeamSeason ts on ts.TeamID = t.TeamID
	inner join dbo.Season s on s.SeasonID = ts.SeasonID
	inner join dbo.PoolConfig pc on pc.PoolConfigID = t.HomePoolConfigID
	inner join dbo.[Pool] p on p.PoolID = pc.PoolID
	inner join dbo.Location l on l.LocationID = p.LocationID
	inner join dbo.League lg on lg.LeagueID = s.LeagueID
	where s.EndDate > GETDATE()	
	and (l.[Address] like '%' + @Address +'%' or @Address is null)
	and	(l.City like @City +'%' or @City is null)
	and	(l.PostalCode = @PostalCode or @PostalCode is null)
	and	(l.RegionID = @RegionID or @RegionID is null)
	and	(t.TeamName like '%' + @TeamName +'%' or @TeamName is null)
		
go

