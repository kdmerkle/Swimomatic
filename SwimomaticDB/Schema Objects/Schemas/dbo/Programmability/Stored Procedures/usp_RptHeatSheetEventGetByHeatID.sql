
if exists (select * from sysobjects where type = 'P' and name = 'usp_RptHeatSheetEventGetByHeatID')
	begin
	    drop procedure dbo.usp_RptHeatSheetEventGetByHeatID
	end
go

create procedure dbo.usp_RptHeatSheetEventGetByHeatID
 /*******************************************************************************************************
 Logical Advantage, LLC
 www.logicaladvantage.com
 704-377-5066
 --------------------------------------------------------------------------------------------------------
 Stored Procedure:	usp_RptHeatSheetEventGetByHeatID
 
 Description:		Selects a ReportHeatSheetEvent record for the given parameters
 
 --------------------------------------------------------------------------------------------------------
 Change Log:
 v1.0.0 - 10/18/2011  Original Release - Generated by LAAF
 ********************************************************************************************************
 To keep the Generator from overwriting this file add the word NOT between the center asterisks ( *NOT* )
 *** DO *NOT* GENERATE ***
 ********************************************************************************************************
 The result of this procedure call is used to populate the ReportHeatSheetEvent Entity
 The Entity expects the following properties to be returned:
   HeatSheetEventID < System.Int32 >
   SwimmerName < System.String >
   EventDescription < System.String >
   LaneNumber < System.Int32 >
   LaneCount < System.Int32 >
   HeatNumber < System.Int32 >
   HeatID < System.Int32 >
 ********************************************************************************************************/
 @HeatID int

as
	declare @LaneCount int
			,@index int = 0
			
	declare @EmptyLanes table (LaneNumber int)

	select @LaneCount = pc.LaneCount
	from dbo.HeatSheetEvent hse
	inner join dbo.HeatSheet hs on hs.HeatSheetID = hse.HeatSheetID
	inner join dbo.PoolConfig pc on pc.PoolConfigID = hs.PoolConfigID
	inner join dbo.Heat h on h.HeatSheetEventID = hse.HeatSheetEventID
	where h.HeatID = @HeatID
	group by pc.LaneCount
	
	while @index < 	@LaneCount
	begin
		insert into @EmptyLanes (LaneNumber) values (@index + 1)
		set @index = @index + 1
	end

;with Heats as (
	select	 hse.HeatSheetEventID
			,h.HeatID
			,h.HeatNumber
			,hse.[Sequence]
			,hswm.Leg
	from dbo.Heat h
	inner join dbo.HeatSheetEvent hse on hse.HeatSheetEventID = h.HeatSheetEventID
	inner join dbo.HeatSwimmer hswm on hswm.HeatID = h.HeatID
	where h.HeatID = @HeatID 
	group by hse.HeatSheetEventID
			,h.HeatID
			,h.HeatNumber
			,hse.[Sequence]
			,hswm.Leg
)
,EmptyLaneHeats as(
	select * 
	from Heats h
	cross join @EmptyLanes el
)

,RealHeats as(
	select
		 hse.HeatSheetEventID
		,hse.[Sequence]
		,h.HeatID
		,h.HeatNumber
		,hswm.LaneNumber
		,hswm.Leg
		,r.ElapsedTime as SeedTime
		,pc.LaneCount
		,ac.[Description] + ' ' + cast(hse.Distance as varchar) + ' ' + substring(u.[Description],0,LEN(u.[Description])) + ' ' + s.[Description] as EventDescription
		,swm.LastName + ', ' + swm.FirstName as SwimmerName
	from dbo.HeatSheetEvent hse
	inner join dbo.Heat h on h.HeatSheetEventID = hse.HeatSheetEventID
	inner join dbo.SwimEvent se on se.SwimEventID = hse.SwimEventID
	inner join dbo.AgeClass ac on ac.AgeClassID = se.AgeClassID
	inner join dbo.Stroke s on s.StrokeID = se.StrokeID
	inner join dbo.HeatSheet hs on hs.HeatSheetID = hse.HeatSheetID
	inner join dbo.PoolConfig pc on pc.PoolConfigID = hs.PoolConfigID
	inner join dbo.UOM u on u.UOMID = pc.UOMID
	inner join dbo.HeatSwimmer hswm on hswm.HeatID = h.HeatID
	inner join dbo.Swimmer swm on swm.SwimmerID = hswm.SwimmerID
	left join dbo.Result r on r.HeatSwimmerID = hswm.HeatSwimmerID
	where h.HeatID = @HeatID
)
	
	select	 ISNULL(rh.HeatSheetEventID, z.HeatSheetEventID) as HeatSheetEventID
			,ISNULL(rh.HeatNumber, z.HeatNumber) as HeatNumber
			,ISNULL(rh.LaneNumber, z.LaneNumber) as LaneNumber
			,ISNULL(rh.Sequence, z.Sequence) as Sequence
			,ISNULL(rh.Leg, z.Leg) as Leg
			,ISNULL(rh.HeatID, z.HeatID) as HeatID
			,rh.SeedTime
			,rh.LaneCount
			,rh.EventDescription
			,rh.SwimmerName
	from EmptyLaneHeats z 
	left join RealHeats rh on z.HeatSheetEventID = rh.HeatSheetEventID 
					and z.LaneNumber = rh.LaneNumber
					and z.HeatNumber = rh.HeatNumber
	order by Sequence
			,HeatNumber
			,LaneNumber
			,Leg	
			
go

